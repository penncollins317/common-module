buildscript {
    apply from: 'dependencies.gradle'
    repositories {
        maven { url 'https://maven.aliyun.com/repository/public' }
    }

    dependencies {
        classpath libraries.'spring-boot-gradle-plugin'
    }
}


subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    sourceCompatibility = project_jdk
    targetCompatibility = project_jdk

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }
    dependencyManagement {
        imports {
            mavenBom libraries.'mybatis-plus-bom'
            mavenBom libraries.'spring-boot-dependencies'
        }
    }
    repositories {
        mavenLocal()
        maven { url 'https://maven.aliyun.com/repository/public/' }
        maven { url 'https://repo.spring.io/milestone' }
        maven { url 'https://repo.spring.io/snapshot' }
        maven {
            name = 'Central Portal Snapshots'
            url = 'https://central.sonatype.com/repository/maven-snapshots/'
        }
        mavenCentral()
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

project("common") {
    dependencies {
        api 'org.springframework.boot:spring-boot-starter-mail'
        api 'org.springframework.boot:spring-boot-starter-validation'
        api 'com.fasterxml.jackson.core:jackson-annotations'
        implementation(libraries.'java-sms-sdk') {
            exclude module: 'commons-logging'
        }
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-aop'
        api 'com.fasterxml.jackson.core:jackson-databind'
        api 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
        implementation 'org.springframework.security:spring-security-core'
        implementation 'jakarta.servlet:jakarta.servlet-api'
        implementation 'com.google.zxing:core:3.5.2'
        implementation 'com.google.zxing:javase:3.5.2'
        api libraries.'mybatis-plus'
        api libraries.'mybatis-spring'
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    }
}


project("user-service") {
    dependencies {
        implementation(project(":common")) {
            exclude group: 'org.springframework.boot', module: 'spring-boot-starter-web'
        }
        implementation 'com.baomidou:mybatis-plus-spring-boot3-starter'
    }
}

project("security") {
    dependencies {
        api project(":common")
        api project(":user-service")
        api libraries.'jjwt'
        implementation libraries.'jaxb-api'
        implementation libraries.'jaxb-core'
        implementation libraries.'jaxb-impl'
        implementation libraries.'commons-codec'
        api 'org.springframework.boot:spring-boot-starter-security'
        api 'org.springframework.boot:spring-boot-starter-web'
        api 'org.springframework.boot:spring-boot-starter-thymeleaf'
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    }
}


project("security-oauth2-server") {
    dependencies {
        implementation project(":common")
        implementation project(":user-service")
        implementation libraries.'spring-security-oauth2-authorization-server'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-mail'
        implementation 'org.springframework.boot:spring-boot-starter-jdbc'
        implementation 'org.springframework.boot:spring-boot-starter-data-redis'
        implementation 'org.springframework.security:spring-security-oauth2-client'
        implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
        implementation 'com.baomidou:mybatis-plus-spring-boot3-starter'
        implementation 'org.postgresql:postgresql'
    }
}

project("security-oauth2-message-resource") {
    dependencies {
        implementation project(":common")
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'org.springframework.security:spring-security-oauth2-client'
        implementation 'org.springframework.security:spring-security-oauth2-jose'
        implementation 'org.springframework.security:spring-security-oauth2-resource-server'
    }
}

project("security-apikeys") {
    dependencies {
        implementation project(":common")
        api project(":security")
        implementation libraries.'servlet-api'
        implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    }
}


project("ai-chat") {
    dependencies {
        implementation project(":common")
        implementation project(":security")
        implementation 'com.baomidou:mybatis-plus-spring-boot3-starter'
        implementation 'com.baomidou:mybatis-plus-jsqlparser'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        api platform("org.springframework.ai:spring-ai-bom:1.0.0-SNAPSHOT")
        api 'org.springframework.ai:spring-ai-openai'
        api 'org.springframework.ai:spring-ai-starter-model-openai'
    }
}

project("oss") {
    dependencies {
        implementation project(":common")
        implementation project(":security")
        api libraries.'aliyun-sdk-oss'
        api libraries.'qiniu-java-sdk'
        api libraries.'minio'
        api libraries.'s3'
        api 'org.springframework.boot:spring-boot-starter-web'
        api 'org.springframework.boot:spring-boot-starter-thymeleaf'
        api 'org.springframework.boot:spring-boot-starter-data-redis'
    }
}

project("starter") {
    dependencies {
        implementation project(":common")
        implementation project(":security")
        implementation project(":ai-chat")
//        implementation project(":security-apikeys")
        implementation project(":oss")
        implementation 'org.postgresql:postgresql'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'com.baomidou:mybatis-plus-spring-boot3-starter'
    }
}
